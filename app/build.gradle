apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.townwang.awemetown"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 3
        versionName "1.0.1"
    }
    signingConfigs {
        config {
            //加载资源
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
            properties.load( inputStream )
            //读取文件
            def sdkDir = properties.getProperty('key.file')
            storeFile file( sdkDir )
            //读取字段
            def key_keyAlias = properties.getProperty( 'keyAlias' )
            def key_keyPassword = properties.getProperty( 'keyPassword' )
            def key_storePassword = properties.getProperty( 'storePassword' )
            //签名密码and 别名
            storePassword key_storePassword
            keyAlias key_keyAlias
            keyPassword key_keyPassword
        }
    }
    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true//打开资源压缩
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true //是否使用zipalign工具压缩
            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled true
            shrinkResources true//打开资源压缩
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true //是否使用zipalign工具压缩
            signingConfig signingConfigs.config
        }
    }

}
// 每次修改运行后自动让 VXP 中的模块`即时生效` ,需要将 (Debug Configurations) - Before Launch - Gradle aware Make - 修改为 :app:installDebug
afterEvaluate {
    installDebug.doLast {
        updateVirtualXposedAPP.execute()
        rebootVirtualXposedAPP.execute()
        launchVirtualXposedAPP.execute()
    }
}
// 更新 VXP 中的 app
task updateVirtualXposedAPP(type: Exec) {
    def pkg = android.defaultConfig.applicationId
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'update', '-e', 'pkg', pkg
}
// 重启 VXP
task rebootVirtualXposedAPP(type: Exec) {
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'reboot'
}
// 重启 VXP 抖音
task launchVirtualXposedAPP(type: Exec) {
    def pkg = 'com.ss.android.ugc.aweme'
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'launch', '-e', 'pkg', pkg
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:27.1.1'
    //卡片布局
    //noinspection GradleCompatible
    implementation 'com.android.support:cardview-v7:27.1.1'
    //Hook
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
    //网络请求
    //Rxjava2 RxAndroid retrofit2
    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    //持久化
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    //Gson
    implementation 'com.google.code.gson:gson:2.8.4'
    //日志
    implementation 'com.townwang:logutils:1.0.0'
    //手机判断
    implementation 'com.townwang:phone:1.0.0'
    //捐赠
    implementation 'com.github.didikee:AndroidDonate:0.1.0'
}